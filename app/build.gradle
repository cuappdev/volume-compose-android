plugins {
    id('com.android.application')
    id('com.google.gms.google-services')
    id('kotlin-android')
    id("kotlin-kapt")
    id("dagger.hilt.android.plugin")
    id('com.apollographql.apollo3').version('3.1.0')
    id('com.google.protobuf').version('0.8.17')
    id('kotlin-parcelize')
}

def secretsPropertiesFile = rootProject.file('secrets.properties')
def secretsProperties = new Properties()
secretsProperties.load(new FileInputStream(secretsPropertiesFile))

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.cornellappdev.volume"
        minSdk 27
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "PROD_ENDPOINT", secretsProperties['PROD_ENDPOINT'])
        buildConfigField("String", "DEV_ENDPOINT", secretsProperties['DEV_ENDPOINT'])
        buildConfigField("String", "FEEDBACK_FORM", secretsProperties['FEEDBACK_FORM'])
        buildConfigField("String", "WEBSITE", secretsProperties['WEBSITE'])

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
        }
        debug {
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "true")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += [
                "-Xjvm-default=all",
                "-opt-in=kotlin.RequiresOptIn"
        ]
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerVersion "1.6.0"
        kotlinCompilerExtensionVersion "1.1.0-beta04"
    }


    namespace 'com.cornellappdev.volume'
}

kapt {
    correctErrorTypes = true
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'com.google.android.material:material:1.8.0-alpha01'
    implementation 'androidx.compose.material3:material3:1.0.0-beta01'
    implementation 'androidx.compose.ui:ui:1.2.1'
    implementation 'androidx.compose.ui:ui-tooling:1.2.1'
    implementation 'androidx.compose.foundation:foundation:1.2.1'
    implementation 'androidx.compose.material:material:1.2.1'
    implementation 'androidx.compose.material:material-icons-core:1.2.1'
    implementation 'androidx.compose.material:material-icons-extended:1.2.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation 'androidx.compose.runtime:runtime-livedata:1.2.1'
    implementation 'androidx.compose.runtime:runtime-rxjava2:1.2.1'
    implementation 'com.apollographql.apollo3:apollo-runtime:3.1.0'
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-compose:2.5.1'

    implementation 'io.coil-kt:coil-compose:2.1.0'
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-core:1.0.0"
    implementation "com.google.protobuf:protobuf-javalite:3.21.1"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:27.0.0')
    implementation 'com.google.firebase:firebase-inappmessaging-display-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-installations:17.0.2'

    // Accompanist
    implementation 'com.google.accompanist:accompanist-pager:0.26.2-beta'
    implementation 'com.google.accompanist:accompanist-pager-indicators:0.26.2-beta'
    implementation "com.google.accompanist:accompanist-webview:0.26.2-beta"

    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.1.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Hilt
    // Implementation and kapt must be in-sync version number
    implementation "com.google.dagger:hilt-android:2.42"
    kapt "com.google.dagger:hilt-android-compiler:2.42"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
}

apollo {
    packageName.set("com.cornellappdev.volume")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.20.0-rc-1"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

